# List all available commands.
default:
    just --list

# Initialize the project.
init: init-setup install init-git-hooks

# Install all required tools.
init-setup:
    pyenv install --skip-existing < .python-version
    pyenv local
    pip install --upgrade pip poetry
    pip install poetry

# Setup git hooks.
init-git-hooks:
    poetry run pre-commit install

# Install python dependencies.
install:
    poetry install

# Run the application locally.
run-dev: run-dev-db
    poetry run uvicorn {{cookiecutter.project_slug}}.main:app --port 8080 --reload

# Run postgres locally in docker.
run-dev-db:
    docker compose up --wait

# Remove local postgres container and volume.
rm-dev-db:
    docker compose rm --force --stop --volumes
    docker compose down --volumes

cloud-sql-proxy:
    ./cloud-sql-proxy pogon-155405:us-central1:task-master-pg13 --unix-socket /tmp/cloudsql

run-prod:
    ENV=prod poetry run uvicorn {{cookiecutter.project_slug}}.main:app --port 8080 --reload

# Run all checks against code.
check: format lint type-check

# Format code with black.
format:
    poetry run black .

# Lint and fix code with ruff.
lint:
    poetry run ruff check --fix .

# Type check with mypy.
type-check:
    poetry run mypy .

# Run all tests.
test: run-dev-db
    poetry run pytest
